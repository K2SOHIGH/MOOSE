#!/usr/bin/env python
# -*- coding: utf-8 -*-

configfile: "config/config.yaml"

import os
import pandas as pd
import yaml
import logging

from utils import utils
from utils import log

wlogger = log.setlogger(__name__)

print(config)
def find_interpro():
    if config["interproscan_dir"]:
        wlogger.info("Interproscan directory : {}".format(config["interproscan_dir"]))
        if os.path.exists(
                os.path.join(config["interproscan_dir"],"interproscan.sh")):
            interproexec = os.path.join(config["interproscan_dir"],"interproscan.sh")
            wlogger.info( "Interproscan location : {}".format(interproexec) )
            return interproexec
        else:
            wlogger.error( "Can't find interproscan executable in {}".format(config["interproscan_dir"]))
            exit(-1)
    wlogger.error("You should specify the location of interproscan with the --interproscan-dir option or run mako-setup-interproscan.")
    exit(-1)
        
        
interproexec = find_interpro()

FASTAS = utils.parse_input(config["INPUT"] , config["extension"] )

rule interproscan_target:
    output:
        os.path.join(config["RESDIR"],"interproscan.tsv"),
    input:
        expand(os.path.join(config["RESDIR"],"tmp","{fasta}","interproscan.{fasta}.tsv"),fasta=FASTAS,),
    shell:
        "cat {input} >> {output}"

rule interproscan_parse:
    output:
        os.path.join(config["RESDIR"],"tmp","{fasta}","interproscan.{fasta}.tsv")
    input:
        os.path.join(config["RESDIR"],"tmp","{fasta}","tmp.{fasta}.tsv")   
    run:
        import pandas as pd
        df = pd.read_csv(str(input),sep="\t",header=None)
        df["wildcards"] = wildcards.fasta
        df.to_csv(str(output),sep="\t",header=None,index=None)


rule interproscan:
    output:
        temp(os.path.join(config["RESDIR"],"tmp","{fasta}","tmp.{fasta}.tsv"))
    input:
        lambda wildcards: FASTAS[wildcards.fasta], 
    params:
        interproscanexec = interproexec,
        prefix = os.path.join(config["RESDIR"],"tmp","{fasta}","tmp.{fasta}"),
        tmp = os.path.join(config["RESDIR"],"tmp","{fasta}","tmp"),
        seqtype = "p" if config["type"] == "proteic" else "n",
        outformat = "TSV" if config["type"] == "proteic" else "GFF3",
        cmd = lambda wildcards,input : "gunzip -c" if str(input).endswith(".gz") else "cat",
    log:
        os.path.join(config["RESDIR"],"logs","interproscan.{fasta}.log")   
    threads:
        10
    shell:        
        "{params.cmd} {input} | sed 's/\*//g' | {params.interproscanexec} -t {params.seqtype} -i - "
        "-b {params.prefix} -f {params.outformat} -T {params.tmp} "
        "-dp --cpu {threads} >> {log} 2>> {log} && touch {output} "

