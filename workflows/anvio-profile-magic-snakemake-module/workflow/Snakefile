from utils import utils
from utils import log 

wlogger = log.setlogger(__name__)
    
wlogger.info("Grabing configuration and input files")

RESDIR = config["RESDIR"]

INPUTS = utils.parse_input(config["INPUT"], None)
CONTIGS = {s:_["fasta"] for s,_ in INPUTS.items()}
BAMS = {s:_["bams"] for s,_ in INPUTS.items()}

if not CONTIGS or not BAMS:    
    wlogger.error("Can't retrieve your contigs or bams from input file")
    exit(-1)

wlogger.info("Input files seems corrects .... continuing ")
wlogger.info("we can start anvi'o magic ! ")    

anviodbs = utils.get_anvio_db_path()

onerror:
    wlogger.error("an error occured during anvio-magic WORKFLOW :(")

rule anvio:
    input:
        expand(
            os.path.join(RESDIR, "{contig}", "anvio.done"), contig = CONTIGS
        )

def get_profiles(wildcards):    
    if len(BAMS[wildcards.contig]) > 1:        
        return os.path.join(RESDIR, "{contig}", "MERGED_PROFILES", "PROFILE.db" )
    return expand( os.path.join(RESDIR,wildcards.contig, "PROFILES", "{bam}", "PROFILE.db"), bam = BAMS[wildcards.contig],)

rule anvio_contigs_workflow:
    output:
        touch(
            temp(
                os.path.join(RESDIR , "{contig}", "anvio.done")
            ),
        )
    input:
        get_profiles,

def aggregate_profiles(wildcards):
    return expand(
        os.path.join(RESDIR, wildcards.contig , "PROFILES", "{bam}" , "PROFILE.db" ),
        bam = BAMS[wildcards.contig],    
    ) 
    
rule anvio_merge_profile:
    output:
        os.path.join(RESDIR, "{contig}", "MERGED_PROFILES", "PROFILE.db" ),
    input:
        profiles = aggregate_profiles,
        db = os.path.join(RESDIR, "{contig}" , "CONTIGS.db"),
    params:
        outdir =  os.path.join(RESDIR, "{contig}", "MERGED_PROFILES"),
        name = lambda wildcards: wildcards.contig.replace(".","_").replace("-","_")
    threads:
        10
    conda:
        "envs/anvio-7.1.yaml"
    shell:
        "anvi-merge "
        "-S {params.name} "
        "-c {input.db} "
        "-o {params.outdir} {input.profiles} "


rule anvio_profile:
    output:
        os.path.join(RESDIR, "{contig}", "PROFILES", "{bam}" , "PROFILE.db" ),
    input:
        bam = lambda wildcards : BAMS[wildcards.contig][wildcards.bam],
        db = os.path.join(RESDIR, "{contig}" , "CONTIGS.db"),
        f = os.path.join(RESDIR , "{contig}" , "anvio-functions.txt"),  
    params:
        outdir = os.path.join(RESDIR, "{contig}" , "PROFILES", "{bam}"),
        name = lambda wildcards: wildcards.contig.replace(".","_").replace("-","_")
    threads:
        10
    conda:
        "envs/anvio-7.1.yaml"
    shell:
        "anvi-profile -i {input.bam} "
        "-W "
        "-c {input.db} "
        "-T {threads} "
        "--output-dir {params.outdir} "
        "--sample-name {params.name} "
        "--cluster-contigs"

rule anvio_function_table:
    output:
        txt = os.path.join(RESDIR , "{contig}" , "anvio-functions.txt"),
        tmp = temp(os.path.join(RESDIR , "{contig}" , "anvio-functions.tmp")),
    input: 
        db = os.path.join(RESDIR, "{contig}" , "CONTIGS.db"),             
        f1 = os.path.join(RESDIR, "{contig}" , "anvio.classify.tsv"),
        f2 = os.path.join(RESDIR, "{contig}" , "cogs.done"),
        f3 = os.path.join(RESDIR, "{contig}" , "pfams.done"),
        f4 = os.path.join(RESDIR, "{contig}" , "keggs.done"),
    conda:
        "envs/anvio-7.1.yaml"
    shell:
        "anvi-export-functions -c {input.db} -o {output.tmp} && "
        "tail -n +2 {output.tmp} | sed 's/$/\t{wildcards.contig}/g' > {output.txt}"


rule anvio_scg_taxonomy:
    output:
        os.path.join(RESDIR , "{contig}" , "anvio.classify.tsv"),
    input:
        ancient(os.path.join(RESDIR, "{contig}" , "CONTIGS.db")), 
    params:     
        mode = "--metagenome-mode" if config["META"] else "",   
        scgdir  = "--scgs-taxonomy-data-dir {}".format(anviodbs) if anviodbs else "",          
    threads:
        10
    conda:
        "envs/anvio-7.1.yaml"
    shell:
        "anvi-run-scg-taxonomy {params.scgdir} -c {input} -T {threads} && "
        "anvi-estimate-scg-taxonomy {params.mode} -c {input} -o {output} --just-do-it "

rule anvio_pfams:
    output:
        os.path.join(RESDIR, "{contig}" , "pfams.done"),
    input:
        ancient(os.path.join(RESDIR, "{contig}" , "CONTIGS.db")), 
    threads:
        10
    params:
        anviodbs = "--pfam-data-dir {}".format(anviodbs) if anviodbs else "",    
    conda:
        "envs/anvio-7.1.yaml"
    shell:
        "anvi-run-pfams {params.anviodbs} "
        "-c {input} --num-threads {threads} && "
        "touch {output} "


rule anvio_keggs:
    output:
        os.path.join(RESDIR, "{contig}" , "keggs.done"),
    input:
        ancient(os.path.join(RESDIR, "{contig}" , "CONTIGS.db")), 
    threads:
        10
    params:
        anviodbs = "--kegg-data-dir {}".format(anviodbs) if anviodbs else "",    
    conda:
        "envs/anvio-7.1.yaml"
    shell:
        "anvi-run-kegg-kofams {params.anviodbs} "
        "-c {input} --num-threads {threads} "
        "--just-do-it && touch {output} "


rule anvio_cogs:
    output:
        os.path.join(RESDIR, "{contig}" , "cogs.done"),
    input:
        ancient(os.path.join(RESDIR, "{contig}" , "CONTIGS.db")),     
    threads:
        10
    params:
        anviodbs = "--cog-data-dir {}".format(anviodbs) if anviodbs else "",
    conda:
        "envs/anvio-7.1.yaml"
    shell:
        "anvi-run-ncbi-cogs {params.anviodbs} "
        "-c {input} "
        "--num-threads {threads} && "
        "touch {output} "


rule anvio_contig_db:
    output:
        os.path.join(RESDIR, "{contig}", "CONTIGS.db"),
    input:
        os.path.join(RESDIR, "{contig}", "tmp.fasta"),        
    conda:
        "envs/anvio-7.1.yaml"
    threads:
        10
    shell:
        "anvi-gen-contigs-database -T {threads} -f {input} -o {output} -n '{wildcards.contig}' && "
        "anvi-run-hmms -c {output} -T {threads} --just-do-it "


rule anvio_contig_reformat:
    output:
        temp(os.path.join(RESDIR, "{contig}", "tmp.fasta")),
    input:
        lambda wildcards : CONTIGS[wildcards.contig],
    params:
        reformat = "false"
    conda:
        "envs/anvio-7.1.yaml"
    threads:
        10
    shell:"""
        if {params.reformat}; then
            anvi-script-reformat-fasta  -o {output} --simplify-names {input} ;
        elif [[ {input} == *.gz ]] ; then 
            gunzip -c {input} > {output}; 
        else 
            cat {input} > {output};
        fi
        """
        